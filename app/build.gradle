apply plugin: 'com.android.application'

//获取当前日期
def static releaseTime() {
    return new Date().format("yyyyMMdd")
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId "com.hjc.base"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        multiDexEnabled true

        ndk {
            // 设置支持的SO库架构(必须设置如下几种,否则X5会初始化失败)
            abiFilters "armeabi", "armeabi-v7a", "x86", "mips"
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
    }

    //签名信息
    signingConfigs {
        release {
            storeFile file('base.jks')
            storePassword '123456'
            keyAlias 'base'
            keyPassword '123456'
        }

        debug {
            storeFile file("base.jks")
            storePassword '123456'
            keyAlias 'base'
            keyPassword '123456'
        }
    }

    buildTypes {
        release {
            //是否混淆
            minifyEnabled false
            //是否移除无用资源
            zipAlignEnabled false
            //前一部分代表系统默认的android程序的混淆文件，该文件已经包含了基本的混淆声明,后者为自定义混淆文件
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.release

            buildConfigField 'String', 'BASE_URL', '"https://yinbao.enjoyqb.com:15050/ybapi/admin-web/services/"'
            buildConfigField 'boolean', 'IS_DEBUG', "false"
        }

        debug {
            minifyEnabled false
            zipAlignEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.debug

            buildConfigField 'String', 'BASE_URL', '"http://test.enjoyqb.com:8888/admin-web/services/"'
            buildConfigField 'boolean', 'IS_DEBUG', "true"
        }
    }

    //指定打包路径和apk名称
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def fileName = "Base_V${variant.versionName}"
            fileName = fileName + "_${releaseTime()}"
            if ('debug' == variant.buildType.name) {
                fileName = fileName + "_debug.apk"
            } else {
                fileName = fileName + "_release.apk"
            }
            outputFileName = fileName
        }
    }

    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }

    lintOptions {
        disable 'GoogleAppIndexingWarning'
        checkReleaseBuilds false
        abortOnError false
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }
}

//解决support包版本不一致问题
configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == 'com.android.support') {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion '28.0.0'
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'

    //分包处理
    implementation 'com.android.support:multidex:1.0.3'

    //图片处理框架
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.9.0'
    implementation 'jp.wasabeef:glide-transformations:4.0.1'

    //注解
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    //路由
    annotationProcessor 'com.alibaba:arouter-compiler:1.2.2'

    //Json
    implementation 'com.google.code.gson:gson:2.8.5'

    //Banner
    implementation 'com.youth.banner:banner:1.4.10'

    //Bugly
    implementation 'com.tencent.bugly:crashreport:3.0.0'
    implementation 'com.tencent.bugly:nativecrashreport:3.6.0'

    //列表适配器
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.46'

    //刷新
    implementation 'com.scwang.smartrefresh:SmartRefreshLayout:1.1.0'
    implementation 'com.scwang.smartrefresh:SmartRefreshHeader:1.1.0'

    //条件选择器
    implementation 'com.contrarywind:Android-PickerView:4.1.8'

    //自定义Toast
    implementation 'com.github.GrenderG:Toasty:1.4.2'

    //Zxing二维码
    implementation 'cn.bingoogolapple:bga-qrcode-zxing:1.3.6'

    implementation project(path: ':BaseLib')
    implementation project(path: ':WebViewLib')
    implementation project(path: ':CameraLib')
}
